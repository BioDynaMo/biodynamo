# This file contains the build setup for simulation projects outside the
# biodynamo repository
# Usage:
#   find_package(BioDynaMo REQUIRED)
#   include(${BDM_USE_FILE})
#   bdm_add_executable(...)

if(UNIX AND NOT APPLE)
  set(LINUX TRUE)
endif()

if(DEFINED ENV{BDM_CMAKE_DIR})
    set(BDM_CMAKE_DIR $ENV{BDM_CMAKE_DIR})
    add_definitions(-DBDM_SRC_DIR=\"$ENV{BDM_SRC_DIR}\")
elseif(LINUX)
    set(BDM_CMAKE_DIR "/snap/biodynamo/current/biodynamo/share/cmake")
    # escaped quotes are mandatory
    add_definitions(-DBDM_SRC_DIR=\"/snap/biodynamo/current/biodynamo/include\")
elseif(APPLE)
    set(BDM_CMAKE_DIR "/usr/local/share/biodynamo/cmake")
    # escaped quotes are mandatory
    add_definitions(-DBDM_SRC_DIR=\"/usr/local/include/biodynamo\")
endif()

# -------------------- find packages ------------------------------------------
# CMake cannot find LLVM's Clang OpenMP library by default (this makes
# find_package(OpenMP) fail). Therefore we manually specify the OpenMP flags.
# Furthermore, rootcling cannot find omp.h by default, so we copy this (with a
# shell script to /usr/local/Cellar/biodynamo, which is brew's default install
# directory), and include this directory. We cannot directly include the
# original directory, because of header conflicts (such as stdint.h)
if(APPLE)
  link_directories(/usr/local/opt/llvm/lib)
  set(OpenMP_C_FLAGS -fopenmp=libomp)
  set(OpenMP_CXX_FLAGS -fopenmp=libomp)
  include_directories(/usr/local/Cellar/biodynamo)
else()
  find_package(OpenMP REQUIRED)
endif()
find_package(Git)
find_package(ROOT REQUIRED COMPONENTS Geom Gui)

find_package(ParaView 5.3 QUIET OPTIONAL_COMPONENTS vtkPVPythonCatalyst)
if(${ParaView_FOUND})
  message(STATUS "Found ParaView")
  include("${PARAVIEW_USE_FILE}")
  add_definitions("-DUSE_CATALYST")
  link_directories($ENV{ParaView_DIR}/../../paraview-5.4)
else()
  message(WARNING "ParaView not found")
endif()

# -------------------- set default build type and compiler flags ---------------
if(NOT CMAKE_CONFIGURATION_TYPES AND NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE Release)
endif()
include("${BDM_CMAKE_DIR}/SetCompilerFlags.cmake")

# -------------------- set rpath options ---------------------------------------
# When building, use the RPATH
set(CMAKE_SKIP_BUILD_RPATH FALSE)           # don't skip the full RPATH for the build tree
set(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH FALSE)
set(CMAKE_INSTALL_RPATH "")

# -------------------- includes -----------------------------------------------
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${BDM_CMAKE_DIR}")
include("${BDM_CMAKE_DIR}/BioDynaMo.cmake")
include(${ROOT_USE_FILE})

set(BDM_REQUIRED_LIBRARIES biodynamo ${ROOT_LIBRARIES} vtkPVPythonCatalyst
    vtkIOParallelXML vtkPVVTKExtensionsDefault vtkPVServerManagerRendering)

# packaged install
if(DEFINED ENV{BDM_CMAKE_DIR} AND LINUX)
  include_directories("$ENV{BDM_CMAKE_DIR}/../../include")
  link_directories("$ENV{BDM_CMAKE_DIR}/../../lib")
elseif(DEFINED ENV{BDM_CMAKE_DIR} AND APPLE)
  include_directories("$ENV{BDM_CMAKE_DIR}/../../../include/biodynamo")
  link_directories("$ENV{BDM_CMAKE_DIR}/../../../lib/biodynamo")
elseif(LINUX)
  include_directories("/snap/biodynamo/current/biodynamo/include")
  include_directories("/snap/biodynamo/current/usr/include")
  link_directories("/snap/biodynamo/current/biodynamo/lib")
elseif(APPLE)
  include_directories("/usr/local/include/biodynamo")
  link_directories("/usr/local/lib/biodynamo")
endif()
