# -----------------------------------------------------------------------------
#
# Copyright (C) The BioDynaMo Project.
# All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
#
# See the LICENSE file distributed with this work for details.
# See the NOTICE file distributed with this work for additional information
# regarding copyright ownership.
#
# -----------------------------------------------------------------------------
# This file contains the build setup for simulation projects outside the
# biodynamo repository
# Usage:
#   find_package(BioDynaMo REQUIRED)
#   include(${BDM_USE_FILE})
#   bdm_add_executable(...)
# -----------------------------------------------------------------------------

# Add our CMake files (e.g. FindXXX.cmake files) to the module path, so that out
# of source build can find them
get_filename_component(CMAKE_DIR ${BDM_USE_FILE} DIRECTORY)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_DIR})
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "$ENV{BDM_INSTALL_DIR}/biodynamo/cmake/")

# Add utils and other methods needed
include(utils)

# Option set when biodynamo was built
SET(cuda_default @cuda@)
SET(opencl_default @opencl@)
SET(dict_default @dict@)
SET(paraview_default @paraview@)
SET(vtune_default @vtune@)

# Options. Turn on with 'cmake -Dmyvarname=ON'.
option(cuda      "Enable CUDA code generation for GPU acceleration" @cuda@)
option(opencl    "Enable OpenCL code generation for GPU acceleration" @opencl@)
option(dict      "Build with ROOT dictionaries" @dict@)
option(paraview  "Enable ParaView" @paraview@)
option(vtune     "Enable VTune performance analysis" @vtune@)

if (NOT "${cuda}" STREQUAL "${cuda_default}")
    MESSAGE(FATAL_ERROR "One of the option which you provided (-Dcuda=${cuda}) is not compatible with the current \
BioDynaMo's build.  BioDynaMo was build by setting -Dcuda to ${cuda_default}. Please change -Dcuda to -Dcuda=${cuda_default}.")
endif()
if (NOT "${opencl}" STREQUAL "${opencl_default}")
    MESSAGE(FATAL_ERROR "One of the option which you provided (-Dopencl=${opencl}) is not compatible with the current \
BioDynaMo's build.  BioDynaMo was build by setting -Dopencl to ${opencl_default}. Please change -Dopencl to -Dopencl=${opencl_default}.")
endif()
if (NOT "${dict}" STREQUAL "${dict_default}")
    MESSAGE(FATAL_ERROR "One of the option which you provided (-Ddict=${dict}) is not compatible with the current \
BioDynaMo's build.  BioDynaMo was build by setting -Ddict to ${dict_default}. Please change -Ddict to -Ddict=${dict_default}.")
endif()
if (NOT "${paraview}" STREQUAL "${paraview_default}")
    MESSAGE(FATAL_ERROR "One of the option which you provided (-Dparaview=${paraview}) is not compatible with the current \
BioDynaMo's build.  BioDynaMo was build by setting -Dparaview to ${paraview_default}. Please change -Dparaview to -Dcuda=${paraview_default}.")
endif()
if (NOT "${vtune}" STREQUAL "${vtune_default}")
    MESSAGE(FATAL_ERROR "One of the option which you provided (-Dvtune=${vtune}) is not compatible with the current \
BioDynaMo's build.  BioDynaMo was build by setting -Dvtune to ${vtune_default}. Please change -Dvtune to -Dvtune=${vtune_default}.")
endif()

# This file contains the build setup for simulation projects outside the
# biodynamo repository
# Usage:
#   find_package(BioDynaMo REQUIRED)
#   include(${BDM_USE_FILE})
#   bdm_add_executable(...)

if(UNIX AND NOT APPLE)
  set(LINUX TRUE)
endif()

if(DEFINED ENV{BDM_CMAKE_DIR})
    set(BDM_CMAKE_DIR $ENV{BDM_CMAKE_DIR})
    add_definitions(-DBDM_SRC_DIR=\"$ENV{BDM_SRC_DIR}\")
else()
  execute_process(COMMAND rm -rf ${CMAKE_CURRENT_BINARY_DIR}/CMakeFiles)
  message(FATAL_ERROR "The BioDynaMo environment is not set up correctly. Please call 'source <path-to-bdm-installation>/bin/thisbdm.sh' and retry this command.")
endif()

# -------------------- find packages ------------------------------------------
find_package(MPI)

find_package(OpenMP REQUIRED)

if (OpenMP_FOUND)
    link_directories(${OpenMP_CXX_LIBRARIES})
endif()

find_package(Git)

if(cuda)
  find_package(CUDA)
endif()

if(opencl)
  find_package(OpenCL)
endif()

if (paraview)
    find_package(GLUT)
endif()

find_package(Numa REQUIRED)
if (NUMA_FOUND)
   include_directories(${NUMA_INCLUDE_DIR})
   link_directories(${NUMA_LIBRARY_DIR})
   add_definitions("-DUSE_NUMA")
   set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} ${NUMA_LIBRARIES})
endif()

if(paraview)
  find_package(ParaView 5.3 QUIET OPTIONAL_COMPONENTS vtkPVPythonCatalyst)
  if(${ParaView_FOUND})
    message(STATUS "Found ParaView")
    include("${PARAVIEW_USE_FILE}")
    add_definitions("-DUSE_CATALYST")
    link_directories($ENV{ParaView_LIB_DIR})
    set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} vtkPVPythonCatalyst
        vtkIOParallelXML vtkPVVTKExtensionsDefault vtkPVServerManagerRendering)
  else()
    message(WARNING "ParaView not found")
    set(paraview OFF CACHE "Build with ParaView support." BOOL)
  endif()
endif()

# paraview version 5.5 (MacOS) does not contain TBB
if (APPLE OR (NOT paraview OR NOT ${ParaView_FOUND}))
  find_package(TBB REQUIRED)
  if (TBB_FOUND)
    include_directories(${TBB_INCLUDE_DIRS})
    link_directories(${TBB_LIBRARIES})
    set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} ${TBB_LIBRARIES})
  endif()
endif()

find_package(ROOT REQUIRED COMPONENTS Geom Gui)
verify_root()
if (dict)
  add_definitions("-DUSE_DICT")
endif()

if (vtune)
    find_package(VTune)
    if(${VTune_FOUND})
        include_directories(${VTUNE_INCLUDE_DIR})
        add_definitions("-DUSE_VTUNE")
        set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} ${VTUNE_LIBRARIES})
    else()
        message(WARNING "VTune not found")
        SET(vtune OFF)
    endif()
endif()

# Link to OpenCL
if (OPENCL_FOUND)
  if (OPENCL_HAS_CXX)
    add_definitions("-DUSE_OPENCL")
    set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} ${OPENCL_LIBRARIES})
  else()
    message(WARNING "OpenCL C++ bindings not found. Please install to make use of OpenCL. "
      "If you think you have installed the C++ bindings correctly, please check if one "
      "of the following environmentals is set correctly (vendor specific):
      - AMD: \t\tAMDAPPSDKROOT
      - NVIDIA: \tCUDA_PATH
      - INTEL: \tINTELOPENCLSDK")
    set(OPENCL_FOUND FALSE)
  endif()
endif()

if (CUDA_FOUND)
  add_definitions("-DUSE_CUDA")
  include_directories(${CUDA_INCLUDE_DIRS} ${CUDA_TOOLKIT_ROOT_DIR})
  set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} ${CUDA_LIBRARIES})
  set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} bdmcuda)
endif()

find_package(ClangTools)
if (NOT ClangTools_FOUND)
    message("clang-tidy and clang-format were not found. Style checks will not be available for this simulation.")
else()
    if ("$ENV{CMAKE_EXPORT_COMPILE_COMMANDS}" STREQUAL "1" OR CLANG_TIDY_FOUND)
      # Generate a Clang compile_commands.json "compilation database" file for use
      # with various development tools, such as Vim's YouCompleteMe plugin.
      # See http://clang.llvm.org/docs/JSONCompilationDatabase.html
      set(CMAKE_EXPORT_COMPILE_COMMANDS 1)
    endif()
endif()

# -------------------- set default build type and compiler flags ---------------
if(NOT CMAKE_CONFIGURATION_TYPES AND NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE Release)
endif()
include("${BDM_CMAKE_DIR}/SetCompilerFlags.cmake")

# -------------------- set rpath options ---------------------------------------
# When building, use the RPATH
set(CMAKE_SKIP_BUILD_RPATH FALSE)           # don't skip the full RPATH for the build tree
set(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH FALSE)
set(CMAKE_INSTALL_RPATH "")

# -------------------- includes -----------------------------------------------
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${BDM_CMAKE_DIR}")
include("${BDM_CMAKE_DIR}/BioDynaMo.cmake")
include(${ROOT_USE_FILE})


# It is used by format/style checks. It needs to be defined here such
# to enable relocation of the scripts.
set(BUILD_SUPPORT_DIR "${BDM_CMAKE_DIR}/../util")
include(CppStyleGuideChecks)

fix_rootcling_omp_issue()

set(BDM_REQUIRED_LIBRARIES ${BDM_REQUIRED_LIBRARIES} biodynamo ${ROOT_LIBRARIES})

include_directories("$ENV{BDM_CMAKE_DIR}/../../include")
link_directories("$ENV{BDM_CMAKE_DIR}/../../lib")
